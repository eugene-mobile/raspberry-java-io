apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'Raspberry Java IO test', 
        	'Implementation-Version': version, 
        	'Main-Class': 'ca.appspace.pi.PirSensor'
        
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile('com.pi4j:pi4j-core:1.0') {
        exclude module: 'pi4j-native'
    }
    compile 'io.reactivex:rxjava:1.0.11'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

configurations { sshAntTask }
 
dependencies { sshAntTask 'org.apache.ant:ant-jsch:1.9.2' }

ant.taskdef(
	name: 'scp',
	description: 'Secure copy file(s) to remote location',
	classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
	classpath: configurations.sshAntTask.asPath
)
 
ant.taskdef(
	name: 'ssh',
	description: 'Execute command on a remote machine using SSG', 
	classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec',
	classpath: configurations.sshAntTask.asPath
)
 
task uploadToRaspberry(dependsOn: ':jar') << {
	ant.scp(
		file: file("build/libs/raspberry-io-1.0.jar"),
		todir: "${scp_user}@${scp_host}:~/raspberry-io/raspberry-io-1.0.jar",
		password: "${scp_password}",
		port: "${scp_port}"
	)
}

task copyToLib( type: Copy ) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}

task uploadLibrariesToRaspberry(dependsOn: ':copyToLib') << {
	for (libFile in fileTree(dir: "$buildDir/libs/lib", include: '*.jar', exclude: 'pi4j-core-*.jar')) {
		ant.scp(
			file: libFile,
			todir: "${scp_user}@${scp_host}:~/raspberry-io/libs/"+libFile.name,
			password: "${scp_password}",
			port: "${scp_port}"
		)
	}
}

